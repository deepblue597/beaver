Pipeline : 

    'pipeline' name=ID '{' 

        kafka=Kafka
        model=Model
        (features=Feature)?
        metrics = Metric
        (target=Target)?
        (plot = Plot)?
        

    '}'
;

Kafka : 

    'kafka' '{' 

        'broker' ':' broker=STRING 
        'input_topic' ':' input_topic=STRING 
        ('output_topic' ':' output_topic = STRING)? 
        'consumer_group' ':' consumer_group = STRING 

    '}'

;

Model : 

    'model' '{' 

        ('ensemble' ':' '{'
            ensemble= Ensemble
            
            '}'
        )?
        ('preprocessing' ':''{' 
            preprocessors+= Preprocessing  
        '}')?
        'type' ':' type = ID 
        'name' ':'  name = ID
        ('probabilistic' ':' proba?= BOOL)?
        ('params' ':' '{' 

            params += Param
        '}')?
        (optimizer = Optimizer)?
    
    
    
    
    '}'
; 

Preprocessing : 

    'preprocesor' '{'

        'name' ':' name = ID  
        ('feature_for_process' ':' process_feature  = STRING ) ?
        ('feature_type' ':' feature_type = ID )?
        ('params' ':' '{' 

            params += Param
        '}')?

    '}'
;

Optimizer : 

    'optimizer' '{'

        'name' ':' name=ID 
        ('params' ':' '{' 

            params += Param
        '}')?

    '}'

;

Ensemble : 
    
        'algorithm' ':' algorithm = ID 
        'number_of_models' ':' num = INT 
        'seed' ':' seed = INT
    
; 

Param : 

    name = ID '=' value =  NumberValue   ; 

NumberValue : FloatValue | IntValue | StringValue | BoolValue;

BoolValue : value = BOOL ; 
StringValue : value+=STRING;
FloatValue: value=MYFLOAT;
IntValue: value=MYINT;
MYINT: /\-?\d+/;
MYFLOAT: /\-?\d+\.\d+([eE][-+]?\d+)?|\-?\d+[eE][-+]?\d+/;


Feature :
    'features' '{'
        ('drop_features' ':' '{' features += ID+ '}')?
        ('generated_features'  ':' '{' assignments*= Assignment '}')? 
    
    '}' 
;


Assignment: variable=ID '=' expression=Expression ';';
Expression: operands=Term (operators=PlusOrMinus operands=Term)*;
PlusOrMinus: '+' | '-';
Term: operands=Factor (operators=MulOrDiv operands=Factor)*;
MulOrDiv: '*' | '/' ;
Factor: (sign=PlusOrMinus)?  op=Operand;
Operand: op_num=NUMBER | op_id=ID | ('(' op_expr=Expression ')');

Metric : 

    'metrics' ':' '{' metrics += ID '}'

;
Target:
    'target' ':' '{'
        'name' ':' name=STRING
        ('mapping' '{' mappings+=Mapping+ '}')?
    '}'
;


Mapping:
    key=ID ':' value=INT;   


Plot : 
    'plot' ':' '{'

        'type' ':' type = ID 
        ('x' ':' x_axis = STRING)? 
    '}'

;



// TODO: Check stats and utils River 
// TODO: Check for multi class classification 
